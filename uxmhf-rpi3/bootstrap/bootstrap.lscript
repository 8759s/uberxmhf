/*
	uxmhf-rpi3 bootstrap image linker script

	author: amit vasudevan (amitvasudevan@acm.org)
*/

MEMORY
{
	ram (rwxai) : ORIGIN = 0x7000, LENGTH = 0x1000
	unaccounted (rwxai) : ORIGIN = 0, LENGTH = 0 /* see section .unaccounted at end */
}

SECTIONS
{
	. = 0x7000;
    
    .text : { 
    	*(.text*) 
    } > ram = 0x0000
    
    .bss : { 
    	*(.bss*) 
	} > ram = 0x0000

    .data : { 
    	*(.data*)
    	*(.rodata*) 
		. = ALIGN(0x1000);
	} > ram = 0x0000

	/DISCARD/ : {
		*(.ARM.attributes)
		*(.comment)
	}

	/* this is to cause the link to fail if there is
	* anything we didn't explicitly place.
	* when this does cause link to fail, temporarily comment
	* this part out to see what sections end up in the output
	* which are not handled above, and handle them.
	*/
	.unaccounted : {
	*(*)
	} >unaccounted
	
}

